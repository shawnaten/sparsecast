repositories {
    maven { url "https://jitpack.io" }
}

apply plugin: 'com.android.application'
apply plugin: 'com.neenbedankt.android-apt'

android {
    compileSdkVersion 22
    buildToolsVersion '22.0.1'
    def getVersionCode = { ->
        def stdout = new ByteArrayOutputStream()
        exec {
            commandLine 'git', 'rev-list', 'HEAD', '--count'
            standardOutput = stdout
        }
        return stdout.toString().trim().toInteger()
    }
    def getVersionName = { ->
        def stdout = new ByteArrayOutputStream()
        exec {
            commandLine 'git', 'describe', '--tags'
            standardOutput = stdout
        }
        return stdout.toString().trim()
    }
    defaultConfig {
        applicationId 'com.shawnaten.simpleweather'
        minSdkVersion 14
        targetSdkVersion 22
        versionCode getVersionCode()
        versionName getVersionName()
        multiDexEnabled true
    }
    buildTypes {
        debug {

        }
        release {
            shrinkResources true
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility
        targetCompatibility JavaVersion.VERSION_1_7
        sourceCompatibility JavaVersion.VERSION_1_7
    }
    productFlavors {
        devLocalhost {
            minSdkVersion 22
        }
        dev {
            minSdkVersion 22
        }
        prod {
            minSdkVersion 14
        }
        prodLocalhost {
            minSdkVersion 14
        }
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])

    compile project(path: ':backend', configuration: 'android-endpoints')

    provided 'org.glassfish:javax.annotation:10.0-b28'
    compile 'com.android.support:appcompat-v7:22.2.0'
    compile 'com.android.support:cardview-v7:22.2.0'
    compile 'com.android.support:recyclerview-v7:22.2.0'
    compile 'com.android.support:gridlayout-v7:22.2.0'
    compile 'com.android.support:palette-v7:22.2.0'
    compile 'com.google.android.gms:play-services:7.5.0'
    compile 'com.squareup.picasso:picasso:2.5.2'
    compile 'com.squareup.okhttp:okhttp-urlconnection:2.4.0'
    compile 'com.squareup.okhttp:okhttp:2.4.0'
    compile 'io.reactivex:rxandroid:0.24.0'
    compile 'com.android.support:multidex:1.0.1'
    compile 'com.google.maps.android:android-maps-utils:0.3.4'
    compile 'com.github.PhilJay:MPAndroidChart:v2.1.0'
    compile 'com.google.android.gms:play-services-ads:7.5.0'

    compile project(':lib')
}

configurations.compile.exclude module: 'guava-jdk5'
